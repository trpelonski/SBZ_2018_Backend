package eventRules;

import com.ftn.SBZ_2018_Projekat.SBZ_2018_Projekat.model.Disease;
import com.ftn.SBZ_2018_Projekat.SBZ_2018_Projekat.model.Diagnostic;
import com.ftn.SBZ_2018_Projekat.SBZ_2018_Projekat.model.DiagnosticDisease;
import com.ftn.SBZ_2018_Projekat.SBZ_2018_Projekat.model.Patient;
import com.ftn.SBZ_2018_Projekat.SBZ_2018_Projekat.events.HeartBeatEvent;
import com.ftn.SBZ_2018_Projekat.SBZ_2018_Projekat.events.UrinationEvent;
import com.ftn.SBZ_2018_Projekat.SBZ_2018_Projekat.events.PatientOxygen;
import com.ftn.SBZ_2018_Projekat.SBZ_2018_Projekat.events.OxygenLevelRisingEvent;
import com.ftn.SBZ_2018_Projekat.SBZ_2018_Projekat.events.OxygenLevelDroppingEvent;

import org.springframework.web.socket.WebSocketSession;
import org.springframework.web.socket.TextMessage;

global WebSocketSession socketSession;

rule "Uvecaj nivo kiseonika"
	no-loop true
	lock-on-active true
	when
		$olr : OxygenLevelRisingEvent ( $patientOxygen : patientOxygen, $amount : amount )
		$po : PatientOxygen ( this == $patientOxygen, $level : level )
	then
		modify($po) { setLevel($level+$amount); }
end

rule "Smanji nivo kiseonika"
	no-loop true
	lock-on-active true
	when
		$old : OxygenLevelDroppingEvent ( $patientOxygen : patientOxygen, $amount : amount )
		$po : PatientOxygen ( this == $patientOxygen, $level : level )
	then
		modify($po) { setLevel($level-$amount); }
end

rule "Problemi sa kiseonikom"
	when
		$po : PatientOxygen ( level < 70, $patientStringId : patientStringId )
		not ( OxygenLevelRisingEvent ( patientOxygen == $po )
			  over window:time(15m) )
	then
		socketSession.sendMessage(new TextMessage("Pacijent "+$patientStringId+" ima problema sa kiseonikom"));
end

rule "Ubrzani srcani ritam"
	when
		Patient ( $id : id, $stringId : stringId )
		Number ( intValue > 25 ) from accumulate(
			HeartBeatEvent ( patientId == $id )
			over window:time(10s),
			count(1)
		)
	then
		socketSession.sendMessage(new TextMessage("Pacijent "+$stringId+" ima ubrzani srcani ritam"));
end

rule "Hitno potrebna dijaliza"
	when
		Patient ( $id : id, $diagnostics : diagnostics, $stringId : stringId )
		Diagnostic ( $diagnosticDiseases : diagnosticDiseases ) from $diagnostics
		DiagnosticDisease ( $disease : disease ) from $diagnosticDiseases
		Disease ( id == 7 ) from $disease	
		Number ( intValue > 10 ) from accumulate(
			HeartBeatEvent ( patientId == $id )
			over window:time(10s),
			count(1)
		)
		Number ( doubleValue < 100.0 ) from accumulate(
			UrinationEvent ( patientId == $id, $amount : amount )
			over window:time(12h),
			sum($amount)
		)	
	then
		socketSession.sendMessage(new TextMessage("Pacijentu "+$stringId+" potrebna dijaliza hitno"));
end

